// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:

    // PC
    Register(in=mixed, load=upd, out=prev);

    // `upd` determines whether we update the register or nor
    Or(a=reset, b=load, out=a1);
    Or(a=a1, b=inc, out=upd);

    // Calculate increment.
    Inc16(in=prev, out=incr);

    // Multiplex possible inputs.
    Mux16(a=prev, b=incr, sel=inc, out=x3);
    Mux16(a=x3, b=in, sel=load, out=x2);
    Mux16(a=x2, b=false, sel=reset, out=mixed);

    // Pass the output to out pin.
    Pass16(in=prev, out=out);
}
